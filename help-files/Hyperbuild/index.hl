/*
 * This is our main "landing page" for our Hyperbuild help files.
 */
h2
  innerValue:Hyperbuild

img
  style:"margin-left:1rem; float:right;max-width: 25%;"
  src:"https://phosphorusfive.files.wordpress.com/2018/03/lego-bricks-png-image-500x332.png"

p
  innerValue:@"Hyperbuild is a build system, allowing you to easily replicate or backup your core. It is arguably
self explaining in nature, and provides few choices - Except the ones necessary to replicate and distribute your
system, and/or modules."

p
  innerValue:@"Hyperbuild should be your first stop if you want to distribute modules, and/or create backups of
your system.."


/*
 * All files at "root" of sub section.
 */
container
  element:ul
  oninit

    /*
     * Retrieving all other Hyperlambda files in the help folder.
     */
    list-files:@HYPERBUILD/help-files/Hyperbuild/
      filter:.hl|.md

    /*
     * Removing "index.hl".
     */
    set:x:@"/@list-files/*/"":regex:/index.hl$/"""

    /*
     * Looping through each Hyperlambda file (except of course "index.hl").
     */
    for-each:x:/@list-files/*

      /*
       * Figuring out filename, without extension, and removing numbering parts.
       */
      split:x:/@_dp/#?name
        =:/
        =:.
      replace:x:/@split/0/-2?name
        src:regex:/[0-9]+ - /

      /*
       * Creating a list item, containing a hyperlink button, which once clicked,
       * loads and displays the specified help file.
       */
      create-widget
        parent:x:/../*/_event?value
        element:li
        widgets
          a
            href:#
            innerValue:x:/@replace?value
            .file:x:/@_dp/#?name
            onclick

              /*
               * Retrieving file name, loading and displaying file, by invoking common
               * widget lambda event, which does the heavy lifting for us.
               */
              get-widget-property:x:/../*/_event?value
                .file
              desktop.help.display-file:x:/@get-widget-property/*/*?value
