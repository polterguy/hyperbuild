
/*
 * File responsible for starting Hyperbuild.
 */





/*
 * Including Micro, adding Awesome Fonts, and serious skin.
 */
micro.css.include





/*
 * Iterating through all "/modules/", and making sure we add each module into
 * the [.modules] lambda.
 */
.modules
list-folders:/modules/
for-each:x:/@list-folders/*?name

  /*
   * Figuring out module name
   */
  split:x:/@_dp?value
    =:/
  add:x:/@.modules
    src:x:/@split/0/-?name





/*
 * Removing license module, if it exists, since license needs to be purchased
 * for each server.
 */
set:x:/@.modules/*/license





/*
 * Adding a select option for each module into "build single module" select dropdown.
 */
for-each:x:/@.modules/*?name
  eval-x:x:/+/*/*/*
  add:x:/../*/create-widget/**/select/=hyperbuild-build-single-module/*/widgets
    src
      option
        innerValue:x:/@_dp?value





/*
 * Adding a checkbox for each module into "main distro" widget.
 */
for-each:x:/@.modules/*?name

  /*
   * Checking if this module should be checked off by default.
   */
  if:x:/@_dp?value
    =:desktop
    or:x:/@_dp?value
      =:micro

    /*
     * Module should be included by default.
     */
    add:x:/..for-each/*/add/*/*/*/*/input
      src
        checked

  /*
   * Adding checkbox.
   */
  eval-x:x:/+/*/*/*/*/*
  add:x:/../*/create-widget/**/widgets/=modules
    src
      label
        widgets
          span
            innerValue:x:/@_dp?value
          input
            type:checkbox
            .data-field:/modules/{0}/
              :x:/@_dp?value





/*
 * Creating our main wireframe widget.
 */
create-widget:hyperbuild-main-container
  class:container
  widgets
    div
      class:row
      widgets
        div
          class:col-100
          widgets
            div
              class:right
              widgets
                div
                  class:strip toolbar
                  widgets
                    a
                      href:/
                      class:button
                      role:button
                      innerValue:@"<span class=""icon-home3""></span>"

    div
      class:row
      widgets
        div
          class:col-100
          widgets
            div
              class:shaded rounded air-inner
              widgets
                h3
                  innerValue:Hyperbuild
                p
                  innerValue:Build a single module. Choose a module you want to build, and click the <em>'Build'</em> button.
                div
                  class:strip fill
                  widgets
                    label
                      innerValue:Module
                    select:hyperbuild-build-single-module
                      widgets
                    button
                      innerValue:Build
                      onclick

                        /*
                         * Figuring out which module user selected, building it,
                         * and downloading it to client.
                         */
                        get-widget-property:hyperbuild-build-single-module
                          value

                        /*
                         * Checking if temp file exists, and if it does, deleting it.
                         */
                        if
                          fetch:x:/0/0?value
                            file-exists::~/temp/{0}.zip
                              :x:/@get-widget-property/*/*?value

                          /*
                           * There exists a temp file with the filename we need to use
                           * for our output file, hence deleting it.
                           */
                          delete-file:~/temp/{0}.zip
                            :x:/@get-widget-property/*/*?value

                        /*
                         * Zipping file, and downloading it to client.
                         */
                        zip:~/temp/{0}.zip
                          :x:/@get-widget-property/*/*?value
                          src:/modules/{0}/
                            :x:/@get-widget-property/*/*?value
                        micro.download.file:~/temp/{0}.zip
                          :x:/@get-widget-property/*/*?value
                hr
                h3
                  innerValue:Main distro
                p
                  innerValue:Build a main distro. Choose which modules you want to include in your main distro.
                div:build-configuration
                  widgets:modules
                div
                  class:right
                  widgets
                    button:hyperbuild-create-main-distro-btn
                      innerValue:Build
                      onclick

                        /*
                         * Making sure we create an obscurer, before we invoke the actual implementation.
                         */
                        create-widgets
                          micro.widgets.cover:hyperbuild-cover-widget
                            message:Please wait while I create your build ...

                        /*
                         * Creating a pingback, which does the actual heavy lifting.
                         */
                        micro.lambda.create-timeout
                          milliseconds:1
                          onfinish

                            /*
                             * Invoking Ajax method that is actually responsible for installing our module.
                             *
                             * Making sure we wrap invocation in a try/catch block.
                             */
                            p5.web.widgets.ajax-events.raise:hyperbuild-create-main-distro-btn
                              .onclick
                            delete-widget:hyperbuild-cover-widget

                      .onclick

                        /*
                         * Serializing form, and creating our build.
                         */
                        micro.form.serialize:build-configuration
                        add:x:/../*/micro.evaluate.file/*/core
                          src:x:@"/@micro.form.serialize/*/""=:bool:true""?name"
                        micro.evaluate.file:@HYPERBUILD/helpers/build.hl
                          include-web-config:bool:true
                          core
